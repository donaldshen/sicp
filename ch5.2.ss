;; (define gcd-machine
;;   (make-machine '(a b t)
;; 		(list (list 'rem remainder) (list '= =))
;; 		'(test-b
;; 		  (test (op =) (reg b) (const 0))
;; 		  (branch (label gcd-done))
;; 		  (assign t (op rem) (reg a) (reg b))
;; 		  (assign a (reg b))
;; 		  (assign b (reg t))
;; 		  (goto (label test-b))
;; 		  gcd-done)))
					;ch5.2.1
(define (make-machine register-names ops controller-text)
  (let ((machine (make-new-machine)))
    (for-each (lambda (register-name)
		((machine 'allocate-register) register-name))
	      register-names)
    ((machine 'install-operations) ops)
    ((machine 'install-instruction-sequence) (assemble controller-text
						       machine))
    machine))
(define (make-register name)
  (let ((contents '*unassigned*)
	(trace-on false))
    (define (dispatch message)
      (cond ((eq? message 'get) contents)
	    ((eq? message 'set)
	     (lambda (value)
	       (if trace-on
		   (begin (newline)
			  (display (list "reg-name: " name))
			  (newline)
			  (display (list "old-value: " contents))
			  (newline)
			  (display (list "new-value: " value))))
	       (set! contents value)))
	    ((eq? message 'trace-on) (set! trace-on true))
	    ((eq? message 'trace-off) (set! trace-on false))
	    (else (error "Unknown request -- REGISTER" message))))
    dispatch))
(define (get-contents register)
  (register 'get))
(define (set-contents! register value)
  ((register 'set) value))
(define (make-stack)
  (let ((s '())
	(number-pushes 0)
	(max-depth 0)
	(current-depth 0))
    (define (push x)
      (set! s (cons x s))
      (set! number-pushes (+ 1 number-pushes))
      (set! current-depth (+ 1 current-depth))
      (set! max-depth (max max-depth current-depth)))
    (define (pop)
      (if (null? s)
	  (error "Empty stack -- POP")
	  (let ((top (car s)))
	    (set! s (cdr s))
	    (set! current-depth (- current-depth 1))
	    top)))
    (define (initialize)
      (set! s '())
      (set! number-pushes 0)
      (set! max-depth 0)
      (set! current-depth 0)
      'done)
    (define (print-statistics)
      (newline)
      (display (list 'total-pushes '= number-pushes
		     'maximum-depth '= max-depth)))
    (define (dispatch message)
      (cond ((eq? message 'push) push)
	    ((eq? message 'pop) (pop))
	    ((eq? message 'initialize) (initialize))
	    ((eq? message 'print-statistics) (print-statistics))
	    (else (error "Unknown request -- STACK" message))))
    dispatch))
(define (pop stack)
  (stack 'pop))
(define (push stack value)
  ((stack 'push) value))
(define (make-new-machine)
  (let ((pc (make-register 'pc))
	(flag (make-register 'flag))
	(stack (make-stack))
	(the-instruction-sequence '())
	(instruction-number 0)
	(trace-on false)
	(current-label 'first-label))
    (let ((the-ops (list (list 'initialize-stack
			       (lambda ()
				 (stack 'initialize)))
			 (list 'print-stack-statistics
			       (lambda ()
				 (stack 'print-statistics)))))
	  (register-table (list (list 'pc pc)
				(list 'flag flag))))
      (define (allocate-register name)
	(if (assoc name register-table)
	    (error "Multiply defined register: " name)
	    (set! register-table
		  (cons (list name (make-register name))
			register-table)))
	'register-allocated)
      (define (lookup-register name)
	(let ((val (assoc name register-table)))
	  (if val
	      (cadr val)
	      ;;(error "Unknown register: " name))))
	      (begin (allocate-register name)
		     (lookup-register name)))))
      (define (execute)
	(let ((insts (get-contents pc)))
	  (if (null? insts)
	      'done
	      (let ((inst (car insts)))
		(begin (cond (trace-on
			      (display current-label)
			      (newline)
			      (display (instruction-text inst))
			      (newline)))
		       ((instruction-execution-proc inst))
		       (set! instruction-number (+ instruction-number 1))
		       (if (or (tagged-list? (instruction-text inst) 'goto)
			       (and (tagged-list? (instruction-text inst) 'branch)
				    (get-contents flag)))
			   (set! current-label
				 (label-exp-label (cadr (instruction-text)))))
		       (execute))))))
      (define (print-instruction-number)
	(display (list "current instruction number = " instruction-number))
	(set! instruction-number 0)
	(newline))
      (define (dispatch message)
	(cond ((eq? message 'start)
	       (set-contents! pc the-instruction-sequence)
	       (execute))
	      ((eq? message 'install-instruction-sequence)
	       (lambda (seq)
		 (set! the-instruction-sequence seq)))
	      ((eq? message 'allocate-register) allocate-register)
	      ((eq? message 'get-register) lookup-register)
	      ((eq? message 'install-operations)
	       (lambda (ops)
		 (set! the-ops (append the-ops ops))))
	      ((eq? message 'stack) stack)
	      ((eq? message 'operations) the-ops)
	      ((eq? message 'print-instruction-number) (print-instruction-number))
	      ((eq? message 'trace-on) (set! trace-on true))
	      ((eq? message 'trace-off) (set! trace-on false))
	      (else (error "Unknown request -- MACHINE" message))))
      dispatch)))
(define (start machine)
  (machine 'start))
(define (get-register-contents machine register-name)
  (get-contents (get-register machine register-name)))
(define (set-register-contents! machine register-name value)
  (set-contents! (get-register machine register-name) value)
  'done)
(define (get-register machine reg-name)
  ((machine 'get-register) reg-name))
					;ch5.2.2
(define (assemble controller-text machine)
  ;;在交回给最初的receive时insts是将每条原inst扩充成pair的表
  (extract-labels controller-text
		  (lambda (insts labels)
		    (update-insts! insts labels machine)
		    insts)))
;;该过程其实是从底部往上构造insts和labels的表
(define (extract-labels text receive)
  (if (null? text)
      (receive '() '())
      (extract-labels (cdr text)
		      (lambda (insts labels)
			(let ((next-inst (car text)))
			  (if (symbol? next-inst)
			      (receive insts
				       ;;此时insts正好保存了从该label下一条inst开始的指令集
				       (cons (make-label-entry next-inst
							       insts)
					     labels))
			      (receive (cons (make-instruction next-inst)
					     insts)
				       labels)))))))
;;labels最终没有直接存储回machine中
(define (update-insts! insts labels machine)
  (let ((pc (get-register machine 'pc))
	(flag (get-register machine 'flag))
	(stack (machine 'stack))
	(ops (machine 'operations)))
    (for-each (lambda (inst)
		(set-instruction-execution-proc!
		 inst
		 (make-execution-procedure (instruction-text inst)
					   labels
					   machine
					   pc
					   flag
					   stack
					   ops)))
	      insts)))
(define (make-instruction text)
  (cons text '()))
(define (instruction-text inst)
  (car inst))
(define (instruction-execution-proc inst)
  (cdr inst))
(define (set-instruction-execution-proc! inst proc)
  (set-cdr! inst proc))
(define (make-label-entry label-name insts)
  (cons label-name insts))
(define (lookup-label labels label-name)
  (let ((val (assoc label-name labels)))
    (if val
	(cdr val)
	(error "Undefined label -- ASSEMBLE" label-name))))
					;ex5.8
(define (extract-labels text receive)
  (if (null? text)
      (receive '() '())
      (extract-labels (cdr text)
		      (lambda (insts labels)
			(let ((next-inst (car text)))
			  (if (symbol? next-inst)
			      (let ((s (assoc next-inst labels)))
				(if s
				    (error "Repeated label name" next-inst)
				    (receive insts
					     (cons (make-label-entry next-inst
								     insts)
						   labels))))
			      (receive (cons (make-instruction next-inst)
					     insts)
				       labels)))))))
					;ch5.2.3
(define (make-execution-procedure inst labels machine pc flag stack ops)
  (let ((tag (car inst)))
    (cond ((eq? tag 'assign)
	   (make-assign inst machine labels ops pc))
	  ((eq? tag 'test)
	   (make-test inst machine labels ops flag pc))
	  ((eq? tag 'branch)
	   (make-branch inst machine labels flag pc))
	  ((eq? tag 'goto)
	   (make-goto inst machine labels pc))
	  ((eq? tag 'save)
	   (make-save inst machine stack pc))
	  ((eq? tag 'restore)
	   (make-restore inst machine stack pc))
	  ((eq? tag 'perform)
	   (make-perform inst machine labels ops pc))
	  (else (error "Unknown instruction type -- ASSEMBLE" inst)))))
(define (make-assign inst machine labels operations pc)
  (let ((target (get-register machine
			      (assign-reg-name inst)))
	(value-exp (assign-value-exp inst)))
    (let ((value-proc (if (operation-exp? value-exp)
			  (make-operation-exp value-exp
					      machine
					      labels
					      operations)
			  (make-primitive-exp (car value-exp)
					      machine
					      labels))))
      (lambda ()
	(set-contents! target (value-proc))
	(advance-pc pc)))))
(define (assign-reg-name assign-instruction)
  (cadr assign-instruction))
(define (assign-value-exp assign-instruction)
  (cddr assign-instruction))
(define (advance-pc pc)
  (set-contents! pc (cdr (get-contents pc))))
(define (make-test inst machine labels operations flag pc)
  (let ((condition (test-condition inst)))
    (if (operation-exp? condition)
	(let ((condition-proc (make-operation-exp condition
						  machine
						  labels
						  operations)))
	  (lambda ()
	    (set-contents! flag (condition-proc))
	    (advance-pc pc)))
	(error "Bas TEST instruction -- ASSEMBLE" inst))))
(define (test-condition test-instruction)
  (cdr test-instruction))
(define (make-branch inst machine labels flag pc)
  (let ((dest (branch-dest inst)))
    (if (label-exp? dest)
	(let ((insts (lookup-label labels
				   (label-exp-label dest))))
	  (lambda ()
	    (if (get-contents flag)
		(set-contents pc insts)
		(advance-pc pc))))
	(error "Bad BRANCH instruction -- ASSEMBLE" inst))))
(define (branch-dest branch-instruction)
  (cadr branch-instruction))

(define (make-goto inst machine labels pc)
  (let ((dest (goto-dest inst)))
    (cond ((label-exp? dest)
	   (let ((insts (lookup-label labels
				      (label-exp-label dest))))
	     (lambda ()
	       (set-contents! pc insts))))
	  ((register-exp? dest)
	   (let ((reg (get-register machine
				    (register-exp-reg dest))))
	     (lambda ()
	       (set-contents! pc (get-contents reg)))))
	  (else (error "Bad GOTO instruction -- ASSEMBLE" inst)))))
(define (goto-dest goto-instruction)
  (cadr goto-instruction))

(define (make-save inst machine stack pc)
  (let ((reg (get-register machine
			   (stack-inst-reg-name inst))))
    (lambda ()
      (push stack (get-contents reg))
      (advance-pc pc))))
(define (make-restore inst machine stack pc)
  (let ((reg (get-register machine
			   (stack-inst-reg-name inst))))
    (lambda ()
      (set-contents! reg (pop stack))
      (advance-pc pc))))
(define (stack-inst-reg-name stack-instruction)
  (cadr stack-instruction))
(define (make-perform inst machine labels operations pc)
  (let ((action (perform-action inst)))
    (if (operation-exp? action)
	(let ((action-proc (make-operation-exp action
					       machine
					       labels
					       operations)))
	  (lambda ()
	    (action-proc)
	    (advance-pc pc)))
	(error "Bad PERFORM instruction -- ASSEMBLE" inst))))
(define (perform-action inst)
  (cdr inst))

(define (make-primitive-exp exp machine labels)
  (cond ((constant-exp? exp)
	 (let ((c (constant-exp-value exp)))
	   (lambda ()
	     c)))
	((label-exp? exp)
	 (let ((insts (lookup-label labels
				    (label-exp-label exp))))
	   (lambda ()
	     insts)))
	((register-exp? exp)
	 (let ((r (get-register machine
				(register-exp-reg exp))))
	   (lambda ()
	     (get-contents r))))
	(else (error "Unknown expression type -- ASSEMBLE" exp))))
(define (register-exp? exp)
  (tagged-list? exp 'reg))
(define (register-exp-reg exp)
  (cadr exp))
(define (constant-exp? exp)
  (tagged-list? exp 'const))
(define (constant-exp-value exp)
  (cadr exp))
(define (label-exp? exp)
  (tagged-list? exp 'label))
(define (label-exp-label exp)
  (cadr exp))
(define (make-operation-exp exp machine labels operations)
  (let ((op (lookup-prim (operation-exp-op exp) operations))
	(aprocs (map (lambda (e)
		       (if (label-exp? e)
			   (error "label can't be operand" e)
			   (make-primitive-exp e machine labels)))
		     (operation-exp-operands exp))))
    (lambda ()
      (apply op (map (lambda (p) (p))
		     aprocs)))))
(define (operation-exp? exp)
  (and (pair? exp)
       (tagged-list? (car exp) 'op)))
;第一项的第二项：((op fuck) ..)
(define (operation-exp-op operation-exp)
  (cadr (car operation-exp)))
(define (operation-exp-operands operation-exp)
  (cdr operation-exp))
(define (lookup-prim symbol operations)
  (let ((val (assoc symbol operations)))
    (if val
	(cadr val)
	(error "Unknown operation -- ASSEMBLE" symbol))))
					;ex5.13
(define (make-machine ops controller-text)
  (let ((machine (make-new-machine)))
    ((machine 'install-operations) ops)
    ((machine 'install-instruction-sequence) (assemble controller-text
						       machine))
    machine))
					;ex5.18
(define (trace-on-register machine register-name)
  ((get-register machine register-name) 'trace-on))
(define (trace-off-register machine register-name)
  ((get-register machine register-name) 'trace-off))
					;ex5.19
(define (make-machine register-names ops controller-text)
  (let ((machine (make-new-machine)))
    (for-each (lambda (register-name)
		((machine 'allocate-register) register-name))
	      register-names)
    ((machine 'install-operations) ops)
    ((machine 'install-instruction-sequence) (assemble controller-text
						       machine))
    ((machine 'install-text) controller-text)
    machine))
(define (make-new-machine)
  (let ((pc (make-register 'pc))
	(flag (make-register 'flag))
	(stack (make-stack))
	(the-instruction-sequence '())
	(instruction-number 0)
	(trace-on false)
	(current-label 'first-label)
	(origin-text '*unassigned*)
	(breakpoints '()))
    (let ((the-ops (list (list 'initialize-stack
			       (lambda ()
				 (stack 'initialize)))
			 (list 'print-stack-statistics
			       (lambda ()
				 (stack 'print-statistics)))))
	  (register-table (list (list 'pc pc)
				(list 'flag flag))))
      (define (allocate-register name)
	(if (assoc name register-table)
	    (error "Multiply defined register: " name)
	    (set! register-table
		  (cons (list name (make-register name))
			register-table)))
	'register-allocated)
      (define (lookup-register name)
	(let ((val (assoc name register-table)))
	  (if val
	      (cadr val)
	      ;;(error "Unknown register: " name))))
	      (begin (allocate-register name)
		     (lookup-register name)))))
      (define (execute)
	(let ((insts (get-contents pc)))
	  (if (null? insts)
	      'done
	      (let ((inst (car insts)))
		(if (not (member (instruction-text inst)
				 breakpoints))
		    (proceed))))))
      (define (print-instruction-number)
	(display (list "current instruction number = " instruction-number))
	(set! instruction-number 0)
	(newline))
      (define (add-breakpoint label n)
	(set! breakpoints
	      (cons (list-ref (member label origin-text)
			      n)
		    breakpoints)))
      (define (proceed)
	(cond (trace-on
	       (display current-label)
	       (newline)
	       (display (instruction-text inst))
	       (newline)))
	((instruction-execution-proc inst))
	(set! instruction-number (+ instruction-number 1))
	(if (or (tagged-list? (instruction-text inst) 'goto)
		(and (tagged-list? (instruction-text inst) 'branch)
		     (get-contents flag)))
	    (set! current-label
		  (label-exp-label (cadr (instruction-text)))))
	(execute))
      (define (dispatch message)
	(cond ((eq? message 'start)
	       (set-contents! pc the-instruction-sequence)
	       (execute))
	      ((eq? message 'install-instruction-sequence)
	       (lambda (seq)
		 (set! the-instruction-sequence seq)))
	      ((eq? message 'allocate-register) allocate-register)
	      ((eq? message 'get-register) lookup-register)
	      ((eq? message 'install-operations)
	       (lambda (ops)
		 (set! the-ops (append the-ops ops))))
	      ((eq? message 'stack) stack)
	      ((eq? message 'operations) the-ops)
	      ((eq? message 'print-instruction-number) (print-instruction-number))
	      ((eq? message 'trace-on) (set! trace-on true))
	      ((eq? message 'trace-off) (set! trace-on false))
	      ;;保存原文，设置初始label
	      ((eq? message 'install-text) (lambda (text)
					     (set! origin-text text)
					     (set! current-label (car text))))
	      ((eq? message 'add-breakpoint) add-breakpoint)
	      ((eq? message 'proceed) (proceed))
	      ((eq? message 'cancel-a-breakpoint)
	       (lambda (label n)
		 (delete (list-ref (member label
					   origin-text)
				   n)
			 origin-text)))
	      ((eq? message 'cancel-all-breakpoints) (set! breakpoints '()))
	      (else (error "Unknown request -- MACHINE" message))))
      dispatch)))

(define (set-breakpoint machine label n)
  ((machine 'add-breakpoint) label n))
(define (proceed-machine machine)
  (machine 'proceed))
(define (cancel-breakpoint machine label n)
  ((machine 'cancel-a-breakpoint) label n))
(define (cancel-all-breakpoints machine )
  (machine 'cancel-all-breakpoints))
(define (delete item lst)
  (cond ((null? lst) '())
	((equal? item (car lst))
	 (delete item (cdr lst)))
	(else (cons (car lst)
		    (delete item (cdr lst))))))
(define (tagged-list? exp sym)
  (eq? (car exp) sym))
